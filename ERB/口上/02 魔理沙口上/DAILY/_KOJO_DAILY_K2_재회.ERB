;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K2_MEET_AGAIN_RATE(대상)
#DIM 대상
RETURN 1000


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K2_MEET_AGAIN_DECISION(대상)
#DIM 대상
#DIM 미마

미마 = NAME_TO_CHARA("미마")

SIF KDVAR:대상:마리사_재회
	RETURN 0

SIF !IS_COUNTRY(CFLAG:대상:소속) || CFLAG:대상:소속 != CFLAG:미마:소속
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(대상, -1, 0) && CHECK_KOJO_DAILY_HAPPEN(미마, -1, 0)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K2_MEET_AGAIN_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K2_MEET_AGAIN(대상)
#DIM 대상
#DIM 미마

미마 = NAME_TO_CHARA("미마")

PRINTFORMW 「어머 어머! 이 정도 옛날의 너는 간단하게 피했었어!」
PRINTFORMW 「우오오! 용서해줘 %ANAME(미마)%님!?」
PRINTFORMW %조사처리(ANAME(대상),"와")% 재회한 %ANAME(미마)%는, 한 때의 제자에게 다시 연습을 시키기로 한 것 같다.
PRINTFORMW 훈련은 하루종일 계속됐다……

CALL COLOR_PRINTW(@"%ANAME(대상)%의 무투가 3 상승했다", 칼라_주의)
ABL:대상:무투 += 3
CALL SKILL_LEARN_BY_NAME(대상, 스킬_장르_PASSIVE, 0, "초성장력")
SIF RESULT
	CALL COLOR_PRINTW(@"%조사처리(ANAME(대상),"는")%＜초성장력＞을 얻었다", 칼라_주의)
CALL ADD_COOLTIME(대상, 3)

KDVAR:대상:마리사_재회 = 1

RETURN 1

