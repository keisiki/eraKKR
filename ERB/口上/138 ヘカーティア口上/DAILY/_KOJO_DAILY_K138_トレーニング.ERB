;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K138_MAGIC_RATE(대상)
#DIM 대상
RETURN 50


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K138_MAGIC_DECISION(대상)
#DIM 대상

SIF !TALENT:MASTER:요술지식
	RETURN 0

SIF KDVAR:대상:헤카티아_트레이닝 == 3
	RETURN 0

SIF CFLAG:대상:호감도 < 1000
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(대상, 1, 0, 1)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K138_MAGIC_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K138_MAGIC(대상)
#DIM 대상

SELECTCASE KDVAR:대상:헤카티아_트레이닝
	CASE 0
		PRINTFORML 가끔씩은 요술에서도 배울까?
		CALL ASK_YN("그렇게 하자", "그만두고 곳")
		IF RESULT == 1
			PRINTFORMW 별로 좋을까……
			KDVAR:대상:헤카티아_트레이닝 = 3
			RETURN 1
		ELSE
			PRINTFORMW 가끔씩은 그것도 좋을까
		ENDIF
		PRINTFORMW 문득 뜰을 보면 %조사처리(ANAME(대상),"가")% 배회하고 있었다
		PRINTFORMW %조사처리(ANAME(대상),"는")% 분명히 마술의 여신이었을 것, 요술에 대한 조예도 깊을 것이다
		PRINTFORMW 조금 지혜를 빌린다고 할까
		PRINTFORMW 「아라, %ANAME(MASTER)%. 어떻게 되었을까?」
		PRINTFORMW 「헤에, 나에게 요술을 가르쳐 주셨으면 하는거네」
		IF ABL:MASTER:요술 > ABL:대상:요술
			PRINTFORMW 「……읏, %조사처리(ANAME(MASTER),"가")% 요술에 대해서는 위가 아니야」
			PRINTFORMW (들)물어 보면 그렇다
			PRINTFORMW 「뭐 좋아요, 두 명의 (분)편이 능숙도 빠를 것입니다」
			PRINTFORMW 그 후 둘이서 요술의 특훈을 했다……
			KDVAR:대상:헤카티아_트레이닝 = 1
		ELSE
			PRINTFORMW 「좋아요, 그렇지만 나의 수행은 스파르타야♪」
			PRINTFORMW %ANAME(대상)%에 충분히 단련할 수 있었다……
			KDVAR:대상:헤카티아_트레이닝 = 2
		ENDIF
	CASE 1
		PRINTFORMW 「%ANAME(MASTER)%, 또 특훈하지 않을까?」
		PRINTFORML %조사처리(ANAME(대상),"가")% 방에 방문해 왔다……
		CALL ASK_YN("그렇게 하자", "지금은 바쁘다")
		IF RESULT == 1
			PRINTFORMW 「그런가, 그러면 또 이번에 와요」
			PRINTFORMW %조사처리(ANAME(대상),"는")% 떠났다……
			RETURN 1
		ELSE
			PRINTFORMW 「그러면 오늘도 노력합시다?」
			PRINTFORMW 그 후 둘이서 요술의 특훈을 했다……
		ENDIF
	CASE 2
		PRINTFORMW 「%ANAME(MASTER)%, 또 단련해 주어요」
		PRINTFORMW %조사처리(ANAME(대상),"가")% 방에 방문해 왔다……
		CALL ASK_YN("그렇게 하자", "지금은 바쁘다")
		IF RESULT == 1
			PRINTFORMW 「그런가, 그러면 또 이번에 와요」
			PRINTFORMW %조사처리(ANAME(대상),"는")% 떠났다……
			RETURN 1
		ELSE
			PRINTFORMW 「그러면 오늘도 엄격하게 가요♪」
			PRINTFORMW %ANAME(대상)%에 충분히 단련할 수 있었다……
		ENDIF
ENDSELECT
CALL PRINT_ADD_EXP(MASTER, EXPNAME:GET_EXP(GETNUM(ABL, "요술")), RAND(20, 40), 1)
CALL PRINT_ADD_EXP(대상, EXPNAME:GET_EXP(GETNUM(ABL, "요술")), RAND(20, 40), 1)

RETURN 1



