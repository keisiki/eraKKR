;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K6_TAIKYOKUKEN_RATE(대상)
#DIM 대상
RETURN 35


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K6_TAIKYOKUKEN_DECISION(대상)
#DIM 대상
RETURN CHECK_KOJO_DAILY_HAPPEN(대상, 1, 0, 1)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K6_TAIKYOKUKEN_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K6_TAIKYOKUKEN(대상)
#DIM 대상
#DIM 경험치

IF CSTR:MASTER:99 == "레밀리아"
	LOCALS:0 = 아가씨
ELSEIF CSTR:MASTER:99 == "파츄리"
	LOCALS:0 = %ANAME(MASTER)%님
ELSE
	LOCALS:0 = %ANAME(MASTER)%씨
ENDIF



IF KDVAR:대상:메이링_태극권 == 0
	PRINTFORML %조사처리(ANAME(MASTER),"가")% 산책을 하던 도중, %조사처리(ANAME(대상),"이")% 마당에서 이상한 움직임을 하고 있는 것을 발견했다.
	PRINTFORMW 대체 무슨 일인걸까 생각하고 있자, 저 쪽도 여기를 알아챈 듯, 이 쪽으로 말을 걸어왔다.
	PRINTFORMW 「%LOCALS:0%. 왜 그러시나요? 여기를 가만히 바라보고 있고」
	PRINTFORMW 「이거 말인가요? 이건 태극권이라고 해서, 예로부터 전해져오던 권법으로부터 만들어진 건강체조 같은 거에요」
	PRINTFORMW 「%LOCALS:0%도 함께 해보는 게 어때요? 기분이 상쾌해질거라고 생각해요」
	PRINTFORML 확실히 최근 운동부족이기도 했고, 함께 해보는 것도 좋을지도 모르겠다……
ELSE
	PRINTFORML %조사처리(ANAME(MASTER),"가")% 산책을 하던 도중, %조사처리(ANAME(대상),"이")% 태극권을 하고 있는 것을 발견했다.
	PRINTFORML 「앗, %LOCALS:0%. 잠깐 어떤가요? 태극권」
ENDIF
PRINTFORML
CALL ASK_YN("해본다", "지금은 됐어")
IF RESULT == 0
	SELECTCASE RAND:100
		CASE IS < 40
			경험치 = GET_EXP(GETNUM(ABL, "무투"))
		CASE IS < 70
			경험치 = GET_EXP(GETNUM(ABL, "방위"))
		CASE IS < 90
			경험치 = GET_EXP(GETNUM(ABL, "지략"))
		CASEELSE
			경험치 = GET_EXP(GETNUM(ABL, "정치"))
	ENDSELECT
	PRINTFORML %조사처리(ANAME(대상),"과")% 태극권을 즐겼다!
	CALL PRINT_ADD_EXP(MASTER, EXPNAME:경험치, RAND:5 + 1, 1)
	PRINTFORMW 「후우, 뭐, 이런 거에요」
	PRINTFORMW 「저는 가끔 여기서 태극권 하고 있으니까, 다음에 또 기분이 내키면 오세요!」
	PRINTFORMW 몸도 산뜻해졌고, 이제 일 힘낼까……
ELSE
	PRINTFORMW 「어라, 유감이네요, 그러면 또 다음 번에」
	PRINTFORMW 유감스럽게도 지금은 바쁘다. %ANAME(대상)%의 권유를 거절했다……
ENDIF

CALL TRAIN_AUTO_ABLUP(대상)

KDVAR:대상:메이링_태극권 = 1

RETURN 1
