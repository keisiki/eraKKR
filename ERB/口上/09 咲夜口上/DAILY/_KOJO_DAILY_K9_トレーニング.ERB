;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K9_TRAINING_RATE(대상)
#DIM 대상
RETURN 40


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K9_TRAINING_DECISION(대상)
#DIM 대상

;레밀리아, 파츄리 이외
SIF GROUPMATCH(CSTR:MASTER:99, "레밀리아", "파츄리")
	RETURN 0


RETURN CHECK_KOJO_DAILY_HAPPEN(대상, 1, 0, 1)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K9_TRAINING_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외


;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K9_TRAINING(대상)
#DIM 대상
#DIM 시간
#DIM 대답
#DIMS 대답문자열 = "왼쪽", "오른쪽", "상", "하"
#DIM 경험치

;대답을 내 둔다
대답 = RAND:4

;오르는 능력도 내 둔다
SELECTCASE RAND:100
	CASE IS < 25
		경험치 = GET_EXP(GETNUM(ABL, "무투"))
	CASE IS < 50
		경험치 = GET_EXP(GETNUM(ABL, "방위"))
	CASE IS < 65
		경험치 = GET_EXP(GETNUM(ABL, "지략"))
	CASE IS < 80
		경험치 = GET_EXP(GETNUM(ABL, "정치"))
	CASE IS < 90
		경험치 = GET_EXP(GETNUM(ABL, "요리"))
	CASEELSE
		경험치 = GET_EXP(GETNUM(ABL, "가창"))
ENDSELECT



SELECTCASE KDVAR:대상:사쿠야_트레이닝
	CASE 0
		PRINTFORMW %조사처리(ANAME(MASTER),"가")% 산책을 하고 있으면, %조사처리(ANAME(대상),"가")% 던지기 나이프의 훈련을 하고 있는 곳을 당했다
		PRINTFORMW %조사처리(ANAME(대상),"는")% 이쪽을 찾아내면, 훈련을 중단해 가까워져 왔다
		PRINTFORML 「%ANAME(MASTER)%. 딱 좋았어요, 나이프의 훈련을 하고 있던 것이지만, 가상적이 없으면 역시 안 돼」
		PRINTFORMW 「조금 교제해 주지 않겠어?」
		PRINTFORMW 가벼운 어조로 말하지만, 손에 가진 그것은 칼날이 붙은 진짜의 나이프다
		PRINTFORML 실패하면 당연 상처나겠지만……
	CASE 1
		PRINTFORMW 또, %ANAME(대상)%의 나이프의 훈련을 당했다
		PRINTFORMW 「아라, 딱 좋았어요」
		PRINTFORMW 「또 교제해 주지 않겠어?」
		PRINTFORML 실패하면 상처나겠지만, 어떻게 하지……
	CASE 2
		PRINTFORMW 또, %ANAME(대상)%의 나이프의 훈련을 당했다
		PRINTFORMW 「아라, 딱 좋았어요」
		PRINTFORMW 「이번에는 교제해 주네요?」
		PRINTFORML 실패하면 상처나겠지만, 어떻게 하지……
ENDSELECT
CALL ASK_YN("교제한다", "위험하기 때문에 그만둔다")
IF RESULT == 1
	PRINTFORMW 「아라, 그렇게?   무기력이군요」
	PRINTFORMW 「어쩔 수 없는, 혼자서 연습하기로 해요」
	PRINTFORML %조사처리(ANAME(대상),"는")% 훈련하러 돌아와 갔다……
	KDVAR:대상:사쿠야_트레이닝 = 2
ELSE
	PRINTFORMW 「후후, 고마워요」
	PRINTFORMW 「그러면, 가요……!」
	PRINTFORML %조사처리(ANAME(대상),"가")% 나이프를 던져 왔다!
	CALL COLOR_PRINTL(@"%조사처리(대답문자열:대답,"를")% 선택해라!", 칼라_경고)
	FOR LOCAL, 0, VARSIZE("대답문자열")
		PRINTFORML [{LOCAL}] %대답문자열:LOCAL%
	NEXT
	TINPUT 2000, -1, 1
	;실패한 패턴
	IF RESULT != 대답
		PRINTFORML %조사처리(ANAME(MASTER),"는")% 회피하려고 시도했다!
		PRINTFORMW 이지만, 나이프는 것의 보기좋게 %ANAME(MASTER)%에 명중했다!
		PRINTFORMW 「조, 조금 괜찮아!?」
		PRINTFORMW 「미안해요. ……그렇지만 덕분에 자신이 붙었어요, 고마워요?」
		PRINTFORMW %ANAME(대상)%에 수당 해 받았다……
		CALL COLOR_PRINTW(@"%조사처리(ANAME(MASTER),"는")% 다쳐 버렸습니다", 칼라_경고)
		CALL ADD_COOLTIME(MASTER, 3)
		CALL PRINT_ADD_EXP(대상, EXPNAME:경험치, RAND:5 + 1, 1)
	;성공
	ELSE
		PRINTFORML %조사처리(ANAME(MASTER),"는")% 회피하려고 시도했다!
		PRINTFORMW %조사처리(ANAME(대상),"가")% 발한 나이프를, 훌륭히 회피해 보였다!
		PRINTFORMW 「아라……상당히 진심으로 던진 것이지만, 설마 피할 수 있다니」
		PRINTFORMW 「나도 아직도네. 좀 더 연습하지 않으면. 교제해 주어 고마워요, 예의를 표한다 원」
		PRINTFORMW %조사처리(ANAME(대상),"는")% 훈련하러 돌아와 갔다……
		CALL PRINT_ADD_EXP(MASTER, EXPNAME:경험치, RAND:5 + 1, 1)
	ENDIF
	KDVAR:대상:사쿠야_트레이닝 = 1
ENDIF


RETURN 1
