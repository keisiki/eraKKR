;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K51_WHERE_IS_TENSHI_RATE(대상)
#DIM 대상
RETURN 1000


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K51_WHERE_IS_TENSHI_DECISION(대상)
#DIM 대상
#DIM 텐시
텐시 = NAME_TO_CHARA("텐시")

;발생이 끝난 상태라면 안 돼
SIF KDVAR:대상:이쿠_않고와
	RETURN 0

;MASTER가 텐시라면 안 돼
SIF MASTER == 텐시
	RETURN 0

;자신의 세력의 포로라면 안 돼
SIF CFLAG:텐시:포로처 == CFLAG:MASTER:소속
	RETURN 0

;텐시가 죽고 있으면 안 돼
SIF CFLAG:텐시:특수상태 == 특수상태_사망
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(대상, 1, 0, 1) && CHECK_KOJO_DAILY_HAPPEN(텐시, 0)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K51_WHERE_IS_TENSHI_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K51_WHERE_IS_TENSHI(대상)
#DIM 대상
#DIM 텐시
텐시 = NAME_TO_CHARA("텐시")

PRINTFORML 방에서 일을 하고 있으면, %조사처리(ANAME(대상),"가")% 방문해 왔다
PRINTFORMW 「%ANAME(MASTER)%씨, 실은 부탁이 있어서」
PRINTFORM 「내가 위법감찰무사직을 하고 있는, 계승자낭님……
CALL COLORPRINT(NAME_FORMAL(텐시), 칼라_주의)
PRINTFORMW 의 일입니다만」
PRINTFORMW 「%PRONOUN(텐시)%를 찾아, 나의 눈이 닿는 곳에 확보해 받고 싶습니다」
PRINTFORML 확보와는 대단히 삼엄한 말투다
PRINTFORML (뜻)이유를 물으면, %조사처리(ANAME(대상),"는")% 말끝을 흐렸다
PRINTFORML 「그……뭐라고 합니까, 계승자낭님은 매우 분방한 (분)편이라서……」
PRINTFORMW 「%PRONOUN(텐시)%를 방목으로 해 두는 것은……트러블의 바탕으로 된다고 합니까」
IF CFLAG:텐시:안면
	PRINTFORMW 확실히, %PRONOUN(텐시)%의 성격을 생각하면, 그것은 분명한 일이다……
ELSE
	PRINTFORMW %PRONOUN(텐시)%와 안면은 없지만, %조사처리(ANAME(대상),"가")% 여기까지 말하는 이상에는 하는 것일 것이다……
ENDIF
PRINTFORMW 「물론, 작전 따위도 있을 것이고, 무리하게라고는 말할 수 없습니다」
PRINTFORMW 「그렇지만, 부디 생각해 주실 수 없을까요?」
PRINTFORML 
CALL ASK_YN("알았다", "무리이다")
SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「감사합니다. 여기저기에 사과하러 가지 않고 끝날 것 같습니다」
		PRINTFORMW 「그럼, 죄송합니다만 잘 부탁드립니다. 나도 가능한 한 협력할테니까」
		PRINTFORML 공손하게 예를 해, %조사처리(ANAME(대상),"는")% 떠났다……
		KDVAR:대상:이쿠_않고와 = 1
	CASE 1
		PRINTFORMW 「그렇습니까……」
		PRINTFORMW 「방법이 없네요. 계승자낭님은 스스로 찾기로 하겠습니다. 바쁘신 중에 실례했습니다」
		PRINTFORML %조사처리(ANAME(대상),"는")% 떠났다……
		KDVAR:대상:이쿠_않고와 = 2
ENDSELECT

RETURN 1

