;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K25_COMPOSE_RATE(대상)
#DIM 대상
RETURN 50


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K25_COMPOSE_DECISION(대상)
#DIM 대상

SIF KDVAR:대상:미스티아_작사
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(대상, 1, 0, 1)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K25_COMPOSE_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K25_COMPOSE(대상)
#DIM 대상
#DIM 선택지, 5
#DIMS 가사, 5, 5
#DIM 정답패턴, 5, 5
#DIM 후보패턴
#DIM 제한시간
#DIM FIRST_LINE
#DIM 현재밀리세컨드

제한시간 = 10000

가사:0:0 = 붙이는지나
가사:0:1 = TO
가사:0:2 = 사람
가사:0:3 = 이
가사:0:4 = 이머리카락을꿰매어라고

가사:1:0 = 따라에땅강아지해
가사:1:1 = 사람
가사:1:2 = 이머리카락을꿰매어라고
가사:1:3 = 환상오늘로는
가사:1:4 = BE

가사:2:0 = 가발에게
가사:2:1 = 해라참고의
가사:2:2 = 사람
가사:2:3 = OR
가사:2:4 = 상식에

가사:3:0 = 사로잡혀서는
가사:3:1 = 인간을
가사:3:2 = NOT TO
가사:3:3 = 개
가사:3:4 = 하려고

가사:4:0 = 여승의가구산
가사:4:1 = BE
가사:4:2 = 생각한것은
가사:4:3 = 안되군요
가사:4:4 = 복습하고

정답패턴:0:0 = 0
정답패턴:0:1 = 0
정답패턴:0:2 = 1
정답패턴:0:3 = 3
정답패턴:0:4 = 0

정답패턴:1:0 = 2
정답패턴:1:1 = 1
정답패턴:1:2 = 2
정답패턴:1:3 = 1
정답패턴:1:4 = 4

정답패턴:2:0 = 4
정답패턴:2:1 = 2
정답패턴:2:2 = 0
정답패턴:2:3 = 4
정답패턴:2:4 = 2

정답패턴:3:0 = 1
정답패턴:3:1 = 4
정답패턴:3:2 = 3
정답패턴:3:3 = 2
정답패턴:3:4 = 1

정답패턴:4:0 = 3
정답패턴:4:1 = 3
정답패턴:4:2 = 4
정답패턴:4:3 = 0
정답패턴:4:4 = 3




PRINTFORMW 「응……정해지지 않는구나……」
PRINTFORMW %조사처리(ANAME(대상),"가")% 뭔가 신음소리를 내고 있는 것이 보였다……
PRINTL
CALL ASK_YN("말을 건다", "방치한다")

IF RESULT == 1
	PRINTFORMW 걱정거리를 방해 하는 것도 멋없을 것이다
	PRINTFORMW 살그머니 해 두기로 했다……
	KDVAR:대상:미스티아_작사 = 1
	RETURN 1
ENDIF

PRINTFORMW 「앗, %ANAME(MASTER)%」
PRINTFORMW 「실은, 새로운 곡의 작사를 하고 있지만……좋은 느낌의 것이 생각해내지 못해서」
PRINTFORMW 「함께 생각해 주지 않겠어?」
PRINTFORML
CALL ASK_YN("좋아", "바쁘다")

IF RESULT == 1
	PRINTFORMW 유감이지만 지금은 바쁘다
	PRINTFORMW 거절했다……
	KDVAR:대상:미스티아_작사 = 1
	RETURN 1
ENDIF

PRINTFORMW 「정말!?   고마워요!」
PRINTFORMW 「자 조금 도전해 봐!   준비할 수 있었다!?」
PRINTFORMW 「간다!」

PRINTL
CALL COLOR_PRINTL("시간내에 좋은 느낌의 가사를 만들어라!!", 칼라_주의)
PRINTL

FIRST_LINE = LINECOUNT

;TINPUT가 남은 시간을 돌려주지 않기 때문에 고육지책
;왜 고육지책일까하고 말하면, GETMILLISECOND는 정밀도가 그다지 좋지 않기 때문에
현재밀리세컨드 = GETMILLISECOND()

FOR LOCAL, 0, VARSIZE("정답패턴", 1)

	FOR LOCAL:1, 0, VARSIZE("가사", 1)
		PRINTFORML [{LOCAL:1}] %가사:LOCAL:(LOCAL:1)%
	NEXT

	$INPUT_LOOP

	TINPUT 제한시간, -1, 1, ""

	;마감 시간
	IF RESULT == -1
		PRINTFORMW 필사적으로 생각해 보았지만, 좋은 시가 떠오르지 않았다……
		PRINTFORMW 「응……역시 어려워?」
		PRINTFORMW 「오늘은 상태가 나쁜 것 같다, 또 이번에 도와 주면 기쁘구나」
		PRINTFORMW 오늘은 연회의 끝으로 하자……
		RETURN 1
	ENDIF
	
	IF !INRANGE(RESULT, 0, 4)
		제한시간 = GETMILLISECOND() - 현재밀리세컨드
		현재밀리세컨드 = GETMILLISECOND()
		GOTO INPUT_LOOP
	ENDIF

	선택지:LOCAL = RESULT
	CLEARLINE LINECOUNT - FIRST_LINE
	제한시간 -= GETMILLISECOND() - 현재밀리세컨드
	현재밀리세컨드 = GETMILLISECOND()

NEXT

PRINTFORMW 만들어 끝냈다!
PRINTFORMW 조속히 %ANAME(대상)%에 노래해 받는다고 하자……

PRINTFORML
FOR LOCAL, 0, VARSIZE("선택지")
	PRINTFORMW 「%가사:LOCAL:(선택지:LOCAL)%～♪」
NEXT
PRINTFORMW

FOR LOCAL, 0, VARSIZE("정답패턴", 1)
	IF 선택지:0 == 정답패턴:LOCAL:0
		후보패턴 = LOCAL
		BREAK
	ENDIF
NEXT

FOR LOCAL, 0, VARSIZE("정답패턴", 1)
	IF 선택지:LOCAL != 정답패턴:후보패턴:LOCAL
		PRINTFORMW 「……응……」
		PRINTFORMW 「뭐랄까, 조금 다를까나」
		PRINTFORMW 「뭐, 그런 펑펑 좋은 것 만들어져도 나의 입장이 없고, 신경쓰지 마」
		PRINTFORMW 「또 도전해 주면 기쁘구나!」
		PRINTFORMW 아무래도 %조사처리(ANAME(대상),"를")% 납득시키는 성과는 아니었던 것 같다
		PRINTFORMW 오늘은 연회의 끝으로 하자……
		RETURN
	ENDIF
NEXT

PRINTFORMW 「굉장해-!   확실히야, 확실히!」
PRINTFORMW 아무래도 %조사처리(ANAME(대상),"를")% 만족시키는 성과의 것을 만들 수 있던 것 같다……
PRINTFORMW 「이것이라면 다음의 라이브로 노래할 수 있다!   즉시 연습해 온다!」
PRINTFORMW 「고마워요, %ANAME(MASTER)%!」
PRINTFORMW %조사처리(ANAME(대상),"는")% 힘차게 달려갔다……
PRINTL
CALL COLOR_PRINTW(@"%ANAME(대상)%의 호감도가 상승했습니다", 칼라_주의)
CALL COLOR_PRINTW(@"%ANAME(MASTER)%의 가창이 8 상승했습니다", 칼라_주의)
CALL COLOR_PRINTW(@"%ANAME(대상)%의 가창이 8 상승했습니다", 칼라_주의)

CFLAG:대상:호감도 += 300
ABL:MASTER:가창 += 8
ABL:대상:가창 += 8

CALL SKILL_LEARN_BY_NAME(대상, 스킬_장르_SP, NO:대상, "휴먼 케이지")
SIF RESULT
	CALL COLOR_PRINTW(@"%조사처리(ANAME(대상),"는")% 스킬 「휴먼 케이지」를 습득했다!", 칼라_주의)

KDVAR:대상:미스티아_작사 = 1

RETURN 1
