;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K61_THINKING_RATE(대상)
#DIM 대상
RETURN 30


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K61_THINKING_DECISION(대상)
#DIM 대상

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("우츠호"), NAME_TO_CHARA("오린"), NAME_TO_CHARA("코이시"))
	RETURN 0

SIF KDVAR:대상:사토리_걱정거리
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(대상, 1, 0, 1)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K61_THINKING_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외


;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K61_THINKING(대상)
#DIM 대상

PRINTFORML 복도에서 %조사처리(ANAME(대상),"와")% 엇갈렸다……

CALL ASK_MULTI("인사한다", "즐거운 일을 생각한다", "괴로운 일을 생각한다", "추잡한 일을 생각한다")

SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「……에에, 안녕하세요」
		PRINTFORMW 특히 무슨 일도 없게 엇갈렸다……
		CFLAG:대상:호감도 += 10
	CASE 1
		PRINTFORMW 「……후후」
		PRINTFORMW 「즐길 수 있었어요」
		PRINTFORMW 마음에 들어 받을 수 있던 것 같다……
		CFLAG:대상:호감도 += 50
	CASE 2
		PRINTFORMW 「……어째서 그런 일을?」
		PRINTFORMW 「내가 우연히 지나갔기 때문에입니까?」
		PRINTFORMW 「마음을 읽을 수 있어도, 이해하기 어려워요」
		PRINTFORMW 마음에 들지 않았던 것 같다……
		CFLAG:대상:호감도 -= 100
	CASE 3
		IF ABL:대상:욕망 >= 랭크역치:랭크_그외:랭크_B
			PRINTFORMW 「……에으음」
			PRINTFORMW 「그것은, 나와 추잡한 일을 하고 싶다는 것입니까?」
			PRINTFORMW 「그런 일이라면, 밤에라도 나의 방에 부디……♥」
			PRINTFORMW %조사처리(ANAME(대상),"는")% 호색인 미소를 띄우고 있었다……
			IF !TALENT:대상:합의
				CALL COLOR_PRINTW(@"%ANAME(대상)%의 합의를 얻었다", 칼라_주의)
				TALENT:대상:합의 = 1
			ENDIF
			CFLAG:대상:호감도 += 100
		ELSEIF IS_LOVER(대상)
			PRINTFORMW 「……에으음」
			PRINTFORMW 「……낮부터 그러한 사고를 흘려 보내는 것은 어떠한 것일까하고」
			PRINTFORMW 「그, 그렇게 말하는 것이 하고 싶은 것이라면, 밤에」
			PRINTFORMW %ANAME(대상)%의 뺨은 홍 밀려들고 있었다……
			CFLAG:대상:호감도 += 50
			CFLAG:대상:의존도 += 50
		ELSEIF IS_SLAVE(대상)
			PRINTFORMW 「……에으음」
			PRINTFORMW 「……나를 사용하고 싶다는 것은 알았습니다만, 낮부터 그러한 사고를 흘려 보내는 것은 어떠한 것일까하고」
			PRINTFORMW 「그, 그렇게 말하는 것이 하고 싶은 것이라면, 밤에」
			PRINTFORMW %ANAME(대상)%의 뺨은 홍 밀려들고 있었다……
			CFLAG:대상:종속도 += 50
			CFLAG:대상:의존도 += 50
		ELSE
			PRINTFORMW 「……하아」
			PRINTFORMW 「대낮부터 그런 일. 불결해요. 경멸합니다」
			PRINTFORMW 서늘한 시선을 향할 수 있었다……
			CFLAG:대상:호감도 -= 100
			CFLAG:대상:종속도 -= 100
			CFLAG:대상:의존도 -= 100
		ENDIF
ENDSELECT
KDVAR:대상:사토리_걱정거리 = 1

RETURN 1
