;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER_RATE(대상)
#DIM 대상
RETURN 1000


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER_DECISION(대상)
#DIM 대상
#DIM 치르노
치르노 = NAME_TO_CHARA("치르노")

SIF CFLAG:대상:소속 != CFLAG:치르노:소속
	RETURN 0

SIF CFLAG:치르노:소속 == CFLAG:치르노:포로처
	RETURN 0

SIF CFLAG:대상:소속 == CFLAG:치르노:포로처
	RETURN 0

SIF KDVAR:대상:대요정_내가대신에
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(대상, -1, 0) && CHECK_KOJO_DAILY_HAPPEN(치르노, -1, 1)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER(대상)
#DIM 대상
#DIM 치르노
치르노 = NAME_TO_CHARA("치르노")

PRINTFORMW 「치…… %ANAME(치르노)%쨩이……!」
PRINTFORML %조사처리(ANAME(대상),"는")% 낭패에 처해있었다. %ANAME(치르노)%가 잡혔기 때문이다.
PRINTFORML %PRONOUN(대상)%에게 있어서, %ANAME(치르노)%는 친구, 혹은 그 이상의 존재다.
PRINTFORMW 만약 무슨 일이 생기면…… 제정신이 아니게 되는 것도 당연했다.
PRINTFORMW 「어떻게든 하지 않으면, 내, 내가…… 내가」
PRINTFORML 자신이.
PRINTFORML 뿌리부터 겁쟁이인 %ANAME(대상)%에게 있어, 자신이 싸운다는 것은 상당한 결단력의 필요한 일이다.
PRINTFORML 그렇지만, %ANAME(치르노)%의 위기다. 그 정도의 용기, 쥐어짤 수 없을 리가 없었다.
PRINTFORMW 「기다려줘 %ANAME(치르노)%쨩…… 바로 도우러 갈테니까!」
CALL COLOR_PRINTW(@"%조사처리(ANAME(대상),"가")% 각오를 결정했습니다", 칼라_주의)
CALL COLOR_PRINTW(@"%ANAME(대상)%의 능력이 상승했습니다", 칼라_주의)

ABL:대상:무투 = MAX(ABL:대상:무투 + 5, 60)
ABL:대상:방위 = MAX(ABL:대상:방위 + 5, 60)
ABL:대상:지략 = MAX(ABL:대상:지략 + 5, 60)

CALL SKILL_LEARN_BY_NAME(대상, 스킬_장르_SP, NO:대상, "굉장한 응원")
SIF RESULT
	CALL COLOR_PRINTW(@"%조사처리(ANAME(대상),"가")% 스킬＜굉장한 응원＞을 습득했습니다", 칼라_주의)

KDVAR:대상:대요정_내가대신에 = 1

RETURN 1
