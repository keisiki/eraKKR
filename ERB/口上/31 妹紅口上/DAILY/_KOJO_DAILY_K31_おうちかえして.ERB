;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K31_PLEASE_GET_BACK_MY_HOME_RATE(대상)
#DIM 대상
RETURN 500


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K31_PLEASE_GET_BACK_MY_HOME_DECISION(대상)
#DIM 대상
#DIM 모코집
모코집 = GET_CITYNUMBER("모코우 집")


SIF 모코집 == -1
	RETURN 0

SIF CITY_OWNER:모코집 == CFLAG:MASTER:소속
	RETURN 0

SIF KDVAR:대상:모코우_집이나해
	RETURN 0

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("테위"), NAME_TO_CHARA("레이센"), NAME_TO_CHARA("카구야"), NAME_TO_CHARA("에이린"))
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(대상, 1, 0, 1)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K31_PLEASE_GET_BACK_MY_HOME_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외


;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K31_PLEASE_GET_BACK_MY_HOME(대상)
#DIM 대상
#DIM 모코집
모코집 = GET_CITYNUMBER("모코우 집")



PRINTFORML 방에서 일을 하고 있으면, %조사처리(ANAME(대상),"가")% 방문해 왔다
PRINTFORMW 「야아, %ANAME(MASTER)%」
PRINTFORMW 「실은 부탁이 있지만……」
PRINTFORMW 「나의 집을 만회했으면 좋다. 역시, 자신 이외의 녀석에게 좋아하게 되는 것은 아니꼽고」
PRINTFORMW 「작전이라든지 있을 것이고, 무리하게라고는 말하지 않지만, 어떨까」
PRINTFORML
CALL ASK_YN("알았다", "무리이다")
SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「고마워요, 살아난다」
		PRINTFORMW 「그러면, 나쁘지만 아무쪼록 부탁한다. 나도 가능한 한 협력하기 때문에」
		PRINTFORML 말할 만큼 말해, %조사처리(ANAME(대상),"는")% 떠났다……
		KDVAR:대상:모코우_집이나해 = 1
	CASE 1
		PRINTFORMW 「그런가」
		PRINTFORMW 「뭐, 어쩔 수 없는가. 바쁜 곳 나빴지요」
		PRINTFORML %조사처리(ANAME(대상),"는")% 떠났다……
		KDVAR:대상:모코우_집이나해 = 2
ENDSELECT
RETURN 1
