;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K23_ALCOHOL_RATE(대상)
#DIM 대상
RETURN 35

;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K23_ALCOHOL_DECISION(대상)
#DIM 대상

SIF CFLAG:대상:특수상태 != 특수상태_방랑
	RETURN 0

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("레이무"), NAME_TO_CHARA("카센"), NAME_TO_CHARA("유카리"), NAME_TO_CHARA("텐시"), NAME_TO_CHARA("유우기"))
	RETURN 0

;한 번 밖에 발생하지 않는 이벤트라면 이런 느낌
SIF KDVAR:대상:스이카_알콜중독
	RETURN 0

;안면이 있으면 안 돼
SIF CFLAG:대상:안면 == 1
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(대상, 0, 0)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K23_ALCOHOL_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K23_ALCOHOL(대상)
#DIM 대상

KDVAR:대상:스이카_알콜중독 = 1


PRINTFORMW 「좀, 거기의 너」
PRINTFORML %조사처리(ANAME(MASTER),"가")% 거리를 순회하고 있으면, 갑자기 말을 걸려졌다
PRINTFORMW 보면, 소녀가 이쪽을 응시하고 있었다
CALL ICPRINT(@"%PRONOUN(대상)%는……확실히<%NAME_FORMAL(대상)%>, 환상향 재수에도 게재되는 유명한 오니다. 왜 이런 곳에?", "W", 칼라_주의)
PRINTFORMW 「시, 실은, 지금 나는 방랑중의 몸으로 말야, 그 정도에 흔들거리고 있지만」
PRINTFORMW 「여비도 없는데 술이 바닥나라고 끝내……표주박으로부터 나오는 술은 질려 버렸고, 이런 이유로, 술 가지고 있지 않아?」
CALL ICPRINT("「그렇네,<일본술 세개>, 주면 답례한다. 거짓말이 아니야, 여하튼 나는 오니니까", "W", 칼라_주의)
PRINTFORML ……설마, 술을 모여들어지는 날이 오려고는 생각하지 않았었다. 그것도 이유가 「여비가 없으니까」란, 많이 한심함
PRINTFORML 하지만, %PRONOUN(대상)%는 오니다. 거짓말은 할 리 없다
PRINTFORML 그런데, 어떻게 할까……

CALL ASK_MULTI_JUDGE("베풀어 준다", ITEM:일본술 >= 3, "무리", 1)

IF RESULT == 1
	PRINTFORMW 「그런가, 그런가……뭐, 어쩔 수 없는가」
	PRINTFORMW 「그러면, 다른 누군가에게 부탁한다고 하자, 방해 해 나빴어요」
	PRINTFORMW 말할 만큼 말해, %조사처리(ANAME(대상),"는")% 안개처럼 사라져 버렸다……
	RETURN 1
ENDIF

PRINTFORMW 「과연!   그러면 조속히 받는다」
PRINTFORMW 말하면서, %PRONOUN(대상)%는 술잔치를 처음으로 끝냈다……
PRINTFORML ·
PRINTFORML ·
PRINTFORML ·
PRINTFORML 일본술도 산페이나 되면 상당한 알코올량이 될 것이지만, %조사처리(ANAME(대상),"는")% 시원스럽게 다 마셔 버렸다
PRINTFORMW 역시 오니라는 일인가……
PRINTFORMW 「후우, 마셨다 마셨다. 역시 일본술은 좋아」
PRINTFORMW 「그러면, 예를 해 줄까. 무엇으로 해?」
PRINTFORML 자, 그럼, 어떻게 하지……
ITEM:일본술 -= 3
CALL ASK_MULTI_JUDGE("힘을 갖고 싶다", 1, "기술을 가르쳐 줘", GET_EMPTY_SKILL_SLOT(MASTER, 스킬_장르_SP) != -1, "금은 없다고 말했군……", 0, "동료가 되어", 1, "소문을 넓혀", 1, "필요없다", 1)

SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「그런 것으로 좋은 것인지?   문제없음이야」
		PRINTFORMW %ANAME(대상)%에 훈련해 받았다……
		CALL COLOR_PRINTW(@"%ANAME(MASTER)%의 무투가 3 상승했다!", 칼라_주의)
		ABL:MASTER:무투 += 3
	CASE 1
		PRINTFORMW 「후응?   아무튼 좋아」
		PRINTFORMW 「그러면, 나의 필살기를 가르쳐 준다고 하자」
		PRINTFORMW 「능숙한 일잘 다루기인」
		CALL SKILL_LEARN_BY_NAME(MASTER, 스킬_장르_SP, NO:대상, "토가쿠시산 던지기")
		CALL COLOR_PRINTW(@"%조사처리(ANAME(MASTER),"는")% 스킬 「토가쿠시산 던지기」를 습득했다!", 칼라_주의)
	CASE 2
		;죽어 분기
	CASE 3
		PRINTFORMW 「앙?   너 동료?」
		PRINTFORMW 「흐음, 뭐 좋아, 너 친절하고, 좋은 일이 있을 듯 하다」
		PRINTFORMW 「나의 힘, 마음껏 사용하면 좋아」
		CALL COLOR_PRINTW(@"%조사처리(ANAME(대상),"가")% 동료가 되었습니다", 칼라_주의)
		CALL CHANGE_COUNTRY(대상, CFLAG:MASTER:소속, 1)
		CFLAG:대상:호감도 += 500
		RETURN 1
	CASE 4
		PRINTFORMW 「소문?   아, 즉 외교적 지위를 얻고 싶은 것이다」
		PRINTFORMW 「나는 안개가 될 수 있고, 그러한 것은 대 자신있어. 뭐, 조금 기다려서 말이야」
		CALL COLOR_PRINTW(@"%조사처리(ANAME(대상),"가")% 소문을 넓은, %ANAME(MASTER)%의 평가가 상승했습니다", 칼라_주의)
		FOR LOCAL, 0, MAX_COUNTRY
			SIF IS_COUNTRY(LOCAL)
				CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:MASTER:소속, 100, -100)
		NEXT
	CASE 5
		PRINTFORMW 「헷?   아, 그래, 그렇다면 아무튼 좋지만」
		PRINTFORMW 「무욕이구나, 이상해」
ENDSELECT
PRINTFORMW 「그러면, 나는 이것으로」
PRINTFORMW %조사처리(ANAME(대상),"는")% 떠나 갔다……
CFLAG:대상:안면 = 1

RETURN 1
