;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K40_PEDDLE_RATE(대상)
#DIM 대상
RETURN 100 + KDVAR:대상:니토리_강매누계액 * 100 > 0

;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K40_PEDDLE_DECISION(대상)
#DIM 대상

SIF KDVAR:대상:니토리_강매 >= 2
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(대상, 1, 0)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K40_PEDDLE_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K40_PEDDLE(대상)
#DIM 대상
#DIM 선택지

IF KDVAR:대상:니토리_강매 > 0
	PRINTFORML ……%조사처리(ANAME(대상),"가")% 방문해 왔다
	PRINTFORMW 「야아 %ANAME(MASTER)%. 또 뭔가 사 주지 않겠어?」
ELSE
	PRINTFORML ……%조사처리(ANAME(대상),"가")% 방문해 왔다
	PRINTFORMW 「야아 %ANAME(MASTER)%. 실은 부탁이 있지만」
	PRINTFORMW 「발명에 필요한 자금을 벌고 싶다. 그렇다는 것으로 뭔가 사 주지 않겠어?」
	PRINTFORMW 아무래도 강매에 온 것 같지만……
ENDIF
PRINTFORML 금:{MONEY}

CALL ASK_MULTI_JUDGE("슈퍼 선전군（금 10000）", MONEY >= 10000, "광역 세뇌 장치（금 15000）", MONEY >= 15000, "미스터 훈련", MONEY >= 20000, "그만둔다", 1)

IF RESULT == 3
	PRINTFORML 「에-?   야」
	PRINTFORML 불필요하다고 전하면, %조사처리(ANAME(대상),"는")% 마지못해 돌아갔다……
	RETURN 1
ENDIF

KDVAR:대상:니토리_강매 = 1
선택지 = RESULT

PRINTFORML 「매번 감사!」

SELECTCASE 선택지
	CASE 0
		PRINTFORML 슈퍼 선전군이 효과를 발휘했다……
		CALL COLOR_PRINTW(@"병 수가 5000증가했다", 칼라_주의)
		COUNTRY_SOLDIER:(CFLAG:MASTER:소속) += 5000
		MONEY -= 10000
		KDVAR:대상:니토리_강매누계액 += 10000
	CASE 1
		PRINTFORML 광역 세뇌 장치가 효과를 발휘했다……
		CALL COLOR_PRINTW(@"동료의 종속도가 상승했다", 칼라_주의)
		FOR LOCAL, 0, CHARANUM
			SIF CFLAG:LOCAL:소속 == CFLAG:MASTER:소속
				CFLAG:LOCAL:종속도 += 500
		NEXT
		MONEY -= 15000
		KDVAR:대상:니토리_강매누계액 += 15000
	CASE 2
		PRINTFORML 미스터 훈련이 효과를 발휘했다……
		CALL COLOR_PRINTW(@"동료가 훈련되었다", 칼라_주의)
		FOR LOCAL, 0, CHARANUM
			IF CFLAG:LOCAL:소속 == CFLAG:MASTER:소속
				EXP:LOCAL:무투경험치 += 20
				EXP:LOCAL:방위경험치 += 20
				EXP:LOCAL:지략경험치 += 20
			ENDIF
			CALL TRAIN_AUTO_ABLUP(LOCAL)
		NEXT
		MONEY -= 20000
		KDVAR:대상:니토리_강매누계액 += 20000
ENDSELECT

SIF KDVAR:대상:니토리_강매누계액 < 100000
	RETURN 1

PRINTFORML 「고마워요!   이것으로 신병기를 만들 수 있다!」
PRINTFORMW %조사처리(ANAME(대상),"는")% 기쁜 듯이 떠났다……
KDVAR:대상:니토리_강매 = 2

CALL COLOR_PRINTW(@"%ANAME(대상)%의 지략이 5 상승했다", 칼라_주의)
ABL:대상:지략 += 5

CALL SKILL_LEARN_BY_NAME(대상, 스킬_장르_CAPTURE, NO:대상, "국일문자 컴프레셔")
IF RESULT
	CALL COLOR_PRINTW(@"%조사처리(ANAME(대상),"가")% 스킬＜국일문자 컴프레셔＞를 습득했습니다", 칼라_주의)
ELSE
	CALL COLOR_PRINTW(@"%ANAME(대상)%의 무투가 5 상승했다", 칼라_주의)
	ABL:대상:지략 += 5
	CALL COLOR_PRINTW(@"%ANAME(대상)%의 방위가 5 상승했다", 칼라_주의)
	ABL:대상:지략 += 5
ENDIF
