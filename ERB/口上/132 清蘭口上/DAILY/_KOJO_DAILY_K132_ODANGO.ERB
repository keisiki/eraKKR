;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@KOJO_DAILY_K132_ODANGO_RATE(대상)
#DIM 대상
RETURN 50

;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@KOJO_DAILY_K132_ODANGO_DECISION(대상)
#DIM 대상

SIF KDVAR:대상:세이란_상품개발
	RETURN 0

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("토요히메"), NAME_TO_CHARA("사구메"), NAME_TO_CHARA("요리히메"), NAME_TO_CHARA("링고"))
	RETURN 0

;호감도 500이 필요
SIF CFLAG:대상:호감도 < 500
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(대상, 1, 0, 1)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@KOJO_DAILY_K132_ODANGO_GENRE(대상)
#DIM 대상
RETURN 데일리_장르_그외

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@KOJO_DAILY_K132_ODANGO(대상)
#DIM 대상

PRINTFORMW %조사처리(ANAME(MASTER),"가")% 일을 하고 있으면, %조사처리(ANAME(대상),"가")% 방문해 왔다
PRINTFORMW 「저기%ANAME(MASTER)%, 조금 도와 주지 않겠어?」
PRINTFORMW 아무래도 경단가게의 신상품을 개발 하고 있는 것 같다
PRINTFORML 자, 그럼, 어떻게 하지……
PRINTL
CALL ASK_YN("교제한다", "지금은 바쁘다")

IF RESULT == 1
	PRINTFORMW 「아-, 그래?」
	PRINTFORMW 「어쩔 수 없네요, 혼자서 어떻게든 해 봐요」
	PRINTFORMW %조사처리(ANAME(대상),"는")% 혼자서 주방에 향했다……
ELSE
	PRINTFORMW 「정말!? 그러면 즉시…」
	PRINTFORMW 둘이서 주방에 향했다…
	IF ABL:MASTER:요리 + ABL:대상:요리 < 70
		PRINTFORMW 좋은 아이디어는 나오지 않았다…
		CALL PRINT_ADD_EXP(MASTER, "요리경험치", RAND(1, 3), 1)
		CALL PRINT_ADD_EXP(대상, "요리경험치", RAND(1, 3), 1)
		CALL TRAIN_AUTO_ABLUP(MASTER)
		CALL TRAIN_AUTO_ABLUP(대상)
	ELSE
		PRINTFORMW 상품의 개발에 성공했다!
		PRINTFORMW 경단은 붐이 되어, 영지민이나 사관들이 빠짐없이 입수했다
		PRINTFORMW 「고마워요%ANAME(MASTER)%! 덕분에 대번성이야!」
		PRINTFORMW 세이란으로부터 매상의 일부를 받았다…
		CALL COLOR_PRINTW(@"금 50000을 얻었습니다", 칼라_주의)
		MONEY += 50000
		CALL COLOR_PRINTW(@"%조사처리(ANAME(대상),"와")% %ANAME(MASTER)%의 요리 솜씨가 올랐다", 칼라_주의)
		ABL:MASTER:요리 += 3
		ABL:대상:요리 += 3
	ENDIF
	KDVAR:대상:세이란_상품개발 = 1
ENDIF

RETURN 1
